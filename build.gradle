
plugins {
    id 'java'
    id 'maven'
    id 'signing'
    id 'net.researchgate.release' version '2.6.0'
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

group = 'com.github.signalr4j'
archivesBaseName = "signalr4j"

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.google.code.gson:gson:2.7'
    compile 'org.java-websocket:Java-WebSocket:1.3.7'

    testCompile 'junit:junit:4.12'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

if(project.findProperty("ossrhUsername") != null && project.findProperty("ossrhPassword") != null) {
    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: project.findProperty("ossrhUsername"), password: project.findProperty("ossrhPassword"))
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: project.findProperty("ossrhUsername"), password: project.findProperty("ossrhPassword"))
                }

                pom.project {
                    name 'signalr4j'
                    packaging 'jar'
                    description 'A java client library for accessing SignalR WebSocket endpoints.'
                    url 'https://github.com/signalr4j/signalr4j'

                    scm {
                        connection 'scm:git:https://github.com/signalr4j/signalr4j.git'
                        developerConnection 'scm:git:https://github.com/signalr4j/signalr4j.git'
                        url 'https://github.com/signalr4j/signalr4j'
                    }

                    licenses {
                        license {
                            name 'Apache 2.0 Licence'
                            url 'https://www.apache.org/licenses/LICENSE-2.0'
                        }
                    }

                    developers {
                        developer {
                            id 'CCob'
                            name 'Ceri Coburn'
                            email 'ceri.coburn@gmail.com'
                        }
                    }

                }
            }
        }
    }

    signing {
        sign configurations.archives
    }

    release {
        failOnCommitNeeded = true
        failOnPublishNeeded = true
        failOnSnapshotDependencies = true
        failOnUnversionedFiles = true
        failOnUpdateNeeded = true
        revertOnFail = true
        preCommitText = ''
        preTagCommitMessage = '[Gradle Release Plugin] - pre tag commit: '
        tagCommitMessage = '[Gradle Release Plugin] - creating tag : '
        newVersionCommitMessage = '[Gradle Release Plugin] - new version commit: '
        tagTemplate = '${version}'
        // May decide to add additional custom tasks here
        buildTasks = ['uploadArchives']
        scmAdapters = [
                net.researchgate.release.GitAdapter
        ]
    }
}


